template header

chid
player
bonus


package eu.trentorise.game.model


// CH1variant - Aumenta del 15% i km fatti a piedi e avrai 50 punti bonus

/*
 CustomData to have   []=used by
    ID is a unique string that identifies the challenge instance
    
  - ch-ID-startChTs [rule] [presentation]
  - ch-ID-endChTs [rule] [presentation]
  - ch-ID-Km_walked_during_challenge [rule] [presentation]
  - ch-ID-target [rule] [presentation]
  - ch-ID-bonus [presentation]
  - ch-ID-type [presentation]
*/

template ch1

rule 'ch-@{chid}-trace'
no-loop
when
	InputData($walk : data["walkDistance"])
	$c: CustomData($now: System.currentTimeMillis(), this['ch-@{chid}-startChTs'] <= $now, this['ch-@{chid}-endChTs'] > $now)
	Player(id == '@{player}') 
then

	Double o = (Double) $c.get('ch-@{chid}-Km_walked_during_challenge');
	if(o == null) {
		$c.put('ch-@{chid}-Km_walked_during_challenge',(Double) $walk);	
	}else {
		$c.put('ch-@{chid}-Km_walked_during_challenge', o + (Double) $walk);
	}
	log('@{chid} update');
	update($c); 
end

rule 'ch-@{chid}-check'
when
	Player(id == '@{player}')
	$c: CustomData($now: System.currentTimeMillis(), this['ch-@{chid}-startChTs'] <= $now, this['ch-@{chid}-endChTs'] > $now, this['ch-@{chid}-Km_walked_during_challenge'] >= this['ch-@{chid}-target'])
	$pc : PointConcept(name == "green leaves")
then
	$pc.setScore($pc.getScore() + @{bonus});
	log('@{chid} success');
	update($pc); 
end

end template