package eu.trentorise.game.model


// CH1variant - Aumenta del 15% i km fatti a piedi e avrai 50 punti bonus

/*
 CustomData to have   []=used by
    ID is a unique string that identifies the challenge instance
    
  - ch-ID-startChTs [rule] [presentation]
  - ch-ID-endChTs [rule] [presentation]
  - ch-ID-Km_walked_during_challenge [rule] [presentation]
  - ch-ID-target [rule] [presentation]
  - ch-ID-bonus [presentation]
  - ch-ID-type [presentation]
  - ch-ID-success [rule] [presentation]
*/
rule 'ch-ID-trace'
no-loop
when
	InputData($walk : data["walkDistance"] != null)
	$c : CustomData($now : System.currentTimeMillis(), this['ch-ID-startChTs'] <= $now, this['ch-ID-endChTs'] > $now, $counter : this['ch-ID-Km_walked_during_challenge'])
	Player(id == 'bruce_wayne') 
then

	if($counter == null) {
		$c.put('ch-ID-Km_walked_during_challenge',(Double) $walk);	
	}else {
		$c.put('ch-ID-Km_walked_during_challenge', (Double) $counter + (Double) $walk);
	}
	log('ch-ID update');
	update($c); 
end

rule 'ch-ID-check'
when
	Player(id == 'bruce_wayne')
	$c : CustomData($now : System.currentTimeMillis(), this['ch-ID-startChTs'] <= $now, this['ch-ID-endChTs'] > $now, this['ch-ID-Km_walked_during_challenge'] >= this['ch-ID-target'], this['ch-ID-success'] == null)
	$pc : PointConcept(name == "green leaves")
then
	$pc.setScore($pc.getScore() + 50);
	$c.put('ch-ID-success',true);
	log('ch-ID success');
	update($pc); 
end